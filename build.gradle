plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id('com.diffplug.spotless') version '6.25.0'
}

group = 'edu.austral.dissis'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.2'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

// Test configuration

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}

// Static code analysis configuration

checkstyle {
    toolVersion = '10.3.3'

    ignoreFailures = false
    maxWarnings = 0
}

// Code formatting configuration

spotless {
    java {
        googleJavaFormat()
    }
}
